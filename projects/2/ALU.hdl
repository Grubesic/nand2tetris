// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Zero the x input if zx is set
    Mux16(a=x, b=false, sel=zx, out=zxOut);

    // Negate the x input if nx is set
    Not16(in=zxOut, out=notX);
    Mux16(a=zxOut, b=notX, sel=nx, out=nxOut);

    // Zero the y input if zy is set
    Mux16(a=y, b=false, sel=zy, out=zyOut);

    // Negate the y input if ny is set
    Not16(in=zyOut, out=notY);
    Mux16(a=zyOut, b=notY, sel=ny, out=nyOut);

    // Perform the operation: addition if f is set, bitwise AND if f is not set
    Add16(a=nxOut, b=nyOut, out=addOut);
    And16(a=nxOut, b=nyOut, out=andOut);
    Mux16(a=andOut, b=addOut, sel=f, out=funcOut);

    // Negate the output if no is set
    Not16(in=funcOut, out=notOut);
    Mux16(a=funcOut, b=notOut, sel=no, out=out, out[15]=msbOut, out[0..7]=firstEight, out[8..15]=lastEight);

    // Set zr (zero result) if out is 0
    Or8Way(in=firstEight, out=lo);
    Or8Way(in=lastEight, out=ro);
    Or(a=lo, b=ro, out=isNotZero);
    Not(in=isNotZero, out=zr);

    // If most significant bit is 1, the number is negative in 2's complement representation.
    And (a=msbOut, b=true, out=ng);
}
